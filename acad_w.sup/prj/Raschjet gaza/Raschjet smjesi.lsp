(defun пересчет_на_доли(список / summ)
  (setq
    summ (apply (function +) список)
  )
  (mapcar (function (lambda(el) (/ el summ)))список)
)

;Пересчет объемных долей смеси в случае некорректной их задачи (сумма объемных долей > или < 1.0)
(defun подгон_долей_смеси_к_1 (состав / доли компоненты)
  (setq	доли	   (доля_1...n состав)
	доли	   (пересчет_на_доли доли)
	компоненты (компонент_1...n состав)
  )
  (mapcar (function cons) компоненты доли)
)
;Расчет молярной массы смеси
(defun mu_см[ri] (состав_об /  мол_массы объемные_доли)
  (setq
    объемные_доли (доля_1...n состав_об)
    мол_массы	  (mu_1...n состав_об)
  )
  (apply '+ (mapcar '* мол_массы объемные_доли))
)

(defun mu_см[gi] (состав_масс /  мол_массы объемные_доли)
  (setq
    объемные_доли (доля_1...n (масс_доли->об_доли состав_масс))
    мол_массы	  (mu_1...n состав_масс)
  )
  (apply '+ (mapcar '* мол_массы объемные_доли))
)
     ;Расчет плотности смеси
(defun ро_cm (mu_cm P_cm T_cm)
  (/ (* mu_cm P_cm) (* 8.314 T_cm))
)

;Расчет низшей теплоты сгорания смеси
(defun Q_рн[ri]_см (состав_об / Q объемные_доли)
  (setq	объемные_доли (доля_1...n состав_об)
	Q	      (Q_рн[ri]_1...n состав_об)
  )
  (apply '+ (mapcar '* Q объемные_доли))
)

 ;Расчет высшей теплоты сгорания смеси

(defun Q_рв[ri]_см (состав_об / Q объемные_доли)
  (setq	объемные_доли (доля_1...n состав_об)
	Q	      (Q_рв[ri]_1...n состав_об)
  )
  (apply '+ (mapcar '* Q объемные_доли))
)
 ;|«Visual LISP© Format Options»
(72 2 5 2 nil "end of" 60 15 0 0 0 T T nil T)
;*** DO NOT add text below the comment! ***|;
