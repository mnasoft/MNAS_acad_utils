;;;Пересчет объемных долей смеси заданных в % в объемные доли смеси
;;;(%->доли '(("O2" . 20.95) ("N2" . 78.09) ("Ar" . 0.93) ("CO2" . 0.03)))
;;;(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003))
(defun %->доли (%)
  (mapcar
    (function
      (lambda (el)
	(cons (car el) (* 0.01 (cdr el)))
      )
    )
    %
  )
)
;;;Пересчет объемных долей смеси в объемные доли смеси в %
;;;(доли->% '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;(("O2" . 20.95) ("N2" . 78.09) ("Ar" . 0.93) ("CO2" . 0.03))
(defun доли->% (доли)
  (mapcar
    (function
      (lambda (el)
	(cons (car el) (* 100. (cdr el)))
      )
    )
    доли
  )
)
;;;(масс_доли->об_доли '(("O2" . 0.231441) ("N2" . 0.755279) ("Ar" . 0.0128245) ("CO2" . 0.000455804)))
;;;(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003))
(defun масс_доли->об_доли (состав_масс	      /
			   mu_масс	      summa[доли_масс/mu_масс]
			   доли_масс	      доли_масс/mu_масс
			   доли_об	      компонент_масс
			   состав_об
			  )
  (setq	доли_масс		 (доля_1...n состав_масс)
	mu_масс			 (mu_1...n состав_масс)
	доли_масс/mu_масс	 (mapcar (function /) доли_масс mu_масс)
	summa[доли_масс/mu_масс] (apply (function +) доли_масс/mu_масс)
	компонент_масс		 (компонент_1...n состав_масс)
	доли_об			 (mapcar
				   (function
				     (lambda (доли_i/mu_i)
				       (/ доли_i/mu_i
					  summa[доли_масс/mu_масс]
				       )
				     )
				   )
				   доли_масс/mu_масс
				 )
	состав_об		 (mapcar (function cons) компонент_масс доли_об)
  )
)


;;;(об_доли->масс_доли '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;(("O2" . 0.231441) ("N2" . 0.755279) ("Ar" . 0.0128245) ("CO2" . 0.000455804))
(defun об_доли->масс_доли (состав_об	    /
			   доли_об	    mu_об
			   доли_об*mu_об    summa[доли_об*mu_об]
			   доля_i*mu_i	    компонент_об
			   доли_масс	    состав_масс
			  )
  (setq	доли_об		     (доля_1...n состав_об)
	mu_об		     (mu_1...n состав_об)
	доли_об*mu_об	     (mapcar (function *) доли_об mu_об)
	summa[доли_об*mu_об] (apply (function +) доли_об*mu_об)
	компонент_об	     (компонент_1...n состав_об)
	доли_масс	     (mapcar
			       (function
				 (lambda (доля_i*mu_i)
				   (/ доля_i*mu_i
				      summa[доли_об*mu_об]
				   )
				 )
			       )
			       доли_об*mu_об
			     )
	состав_масс	     (mapcar (function cons) компонент_об доли_масс)
  )
  состав_масс
)
;;;Нахождение молекулярной массы i-тового компонента.
;;;(mu_i "C3")
;;;0.044094
(defun mu_i (i / формула_i молямные_массы_элементов)
  (setq	формула_i		 (cdr (assoc "Формула-состав" (cdr (assoc i (gas_tbl)))))
	молямные_массы_элементов
				 (mapcar
				   (function
				     (lambda (el)
				       (* (cdr (assoc (car el) (mendeleev_tbl))) (cadr el))
				     )
				   )
				   формула_i
				 )
  )
  (apply (function +) молямные_массы_элементов)
)
;;;(mu_1...n '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;(0.032 0.028016 0.039944 0.04401)
(defun mu_1...n (состав )
  (mapcar (function mu_i) (компонент_1...n состав))
)
;;;(компонент_i "O2" '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;"O2"
;;;(компонент_i "C1" '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;nil
(defun компонент_i (i состав)
  (car (assoc i состав))
)
;;;(компонент_1...n  '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;("O2" "N2" "Ar" "CO2")
(defun компонент_1...n(состав)
  (mapcar (function car) состав)
)
;;;(доля_i "O2" '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;0.2095
(defun доля_i(i состав)
  (cdr(assoc i состав))
)
;;;(доля_1...n '(("O2" . 0.2095) ("N2" . 0.7809) ("Ar" . 0.0093) ("CO2" . 0.0003)))
;;;(0.2095 0.7809 0.0093 0.0003)
(defun доля_1...n(состав)
  (mapcar (function cdr) состав)
)

(defun комп_доля_i (i состав)
  (cond
    ((assoc i состав))
    (t
     (cons i 0.0)
    )
  )
)

(defun комп_доля_1...n (элементы состав)
  (mapcar
    (function
      (lambda (i)
	(cond
	  ((assoc i состав))
	  (t
	   (cons i 0.0)
	  )
	)
      )
    )
    элементы
  )
)


;;;(Q_рн[ri]_i "C1")
;;;33.41
(defun Q_рн[ri]_i (i)
  (cdr (assoc "Qн" (cdr (assoc i (gas_tbl)))))
)
;;;(Q_рн[ri]_1...n '(("C1" . 0.969543) ("C2" . 0.00507614) ("C3" . 0.00507614) ("N2" . 0.0126904) ("CO2" . 0.00253807) ("O2" . 0.00507614)))
;;;(33.41 59.85 86.53 0.0 0.0 0.0)
(defun Q_рн[ri]_1...n (состав / компоненты el)
  (setq компоненты (компонент_1...n состав))
  (mapcar (function (lambda (el) (Q_рн[ri]_i el))) компоненты)
)
;;;(Q_рв[ri]_i "C1")
;;;37.1
(defun Q_рв[ri]_i (i)
  (cdr (assoc "Qв" (cdr (assoc i (gas_tbl)))))
)
;;;(Q_рв[ri]_1...n '(("C1" . 0.969543) ("C2" . 0.00507614) ("C3" . 0.00507614) ("N2" . 0.0126904) ("CO2" . 0.00253807) ("O2" . 0.00507614)))
;;;(37.1 65.38 93.98 0.0 0.0 0.0)
(defun Q_рв[ri]_1...n (состав / компоненты el)
  (setq компоненты (компонент_1...n состав))
  (mapcar (function (lambda (el) (Q_рв[ri]_i el))) компоненты)
)
;|«Visual LISP© Format Options»
(72 2 5 2 nil "end of" 60 15 0 0 0 T T nil T)
;*** DO NOT add text below the comment! ***|;
